group = 'com.intel.rfid'
version = '1.0'

apply plugin: 'java'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://repo.eclipse.org/content/repositories/paho-releases/' }
}

configurations {
    runtime.exclude group: 'org.slf4j', module: 'slf4j-simple'
    runtime.exclude group: 'org.slf4j', module: 'slf4j-jdk14'
}

dependencies {

    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.7'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.jmdns:jmdns:3.5.4'
    compile 'jline:jline:2.14.6'
    compile 'org.apache.sshd:apache-sshd:1.7.0'
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.0'
    compile 'org.bouncycastle:bcprov-jdk15on:1.60'
    compile 'org.bouncycastle:bcpkix-jdk15on:1.60'
    compile 'org.eclipse.jetty:jetty-servlet:9.4.+'
    compile 'org.eclipse.jetty:jetty-server:9.4.+'

    testCompile 'junit:junit:4.11'
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }
}

// define the common configuration copy spec files as
// a reference to be included in other copy tasks
CopySpec baseFunctionalityCS = copySpec {
    from("bin") {
        fileMode = 0755
    }
    into("config") {
        from "config"
    }
    into("lib") {
        from jar
        from configurations.runtime
    }
}

task buildTar(type: Tar, dependsOn: classes) {
    baseName "${project.name}"
    compression Compression.GZIP
    into "${project.name}"
    with baseFunctionalityCS
}

